repositories {
    maven { url = "https://maven.terraformersmc.com/" } // ModMenu, EMI
    maven { url = "https://maven.shedaniel.me" } // REI
}

dependencies {
    compileOnly(project(":common"))
    
    modImplementation("net.fabricmc:fabric-loader:${fabricLoaderVersion}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabricVersion}")
    
    modImplementation("com.tterrag.registrate_fabric:Registrate:${rootProject.registrateFabricVersion}")
    modImplementation("com.simibubi.create:create-fabric-${rootProject.minecraftVersion}:${createFabricVersion}")
    
    // Mods with compat
    modImplementation("com.terraformersmc:modmenu:${modmenuVersion}")
    modCompileOnly("dev.emi:emi-fabric:${emiVersion}+${rootProject.minecraftVersion}:api")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${reiVersion}")
    
    if (recipeViewer == "emi") {
        modRuntimeOnly("dev.emi:emi-fabric:${emiVersion}+${rootProject.minecraftVersion}")
    } else if (recipeViewer == "rei") {
        modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${reiVersion}")
    }
    
    // Runtime only mods
    modRuntimeOnly("maven.modrinth:sodium:${sodiumVersion}")
    modRuntimeOnly("maven.modrinth:indium:${indiumVersion}")
    
    // Runtime only mods for testing
    modRuntimeOnly("maven.modrinth:resourceful-lib:YzWNEutS")
    modRuntimeOnly("maven.modrinth:resourceful-config:PlEObkl9")
    modRuntimeOnly("maven.modrinth:botarium:YgOh7n9M")
    modRuntimeOnly("maven.modrinth:ad-astra:DruFc2hK")
}

compileJava {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-ACNA_FABRIC']
}

var modId = rootProject.modId

loom {
    runs {
        datagen {
            inherit(client)
            vmArg("-Dfabric-api.datagen")
            vmArg("-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}")
            vmArg("-Dporting_lib.datagen.existing_resources=${file("../common/src/main/resources")}")
            vmArg("-Dfabric-api.datagen.modid=${modId}")
            
            runDir("build/datagen")
        }
    }    
}
