plugins {
	id "dev.architectury.loom" version "1.2-SNAPSHOT"
	id "maven-publish"
}

version = project.mod_version
group = project.maven_group

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
archivesBaseName = project.archives_base_name

repositories {
	mavenLocal()
	maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
	maven { url = "https://maven.blamejared.com/" } // JEI
	maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
	maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
	maven { url = "https://api.modrinth.com/maven" } // LazyDFU, JEI runtime
	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url = "https://mvn.devos.one/snapshots/" } // Create, Porting Lib, Forge Tags, Milk Lib, Registrate
	maven { url = "https://cursemaven.com" } // Forge Config API Port
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
	maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
	maven { url = "https://maven.tterrag.com/" } // Flywheel
	maven {
		// Location of the maven that hosts Mine and Team Resourceful's files.
		name = "Resourceful Bees Maven"
		url = "https://maven.resourcefulbees.com/repository/maven-public/"
	}
	maven {
		url "https://squiddev.cc/maven/"
		content {
			includeGroup("cc.tweaked")
			includeModule("org.squiddev", "Cobalt")
		}
	}
}

loom {
	mods {
		"create-new-age" {
			sourceSet sourceSets.main
		}
	}
	forge {
		mixinConfigs = [
				"create_new_age.mixins.json"
		]
	}
}

dependencies {
	minecraft("com.mojang:minecraft:${minecraft_version}")
	mappings loom.officialMojangMappings()

	forge "net.minecraftforge:forge:${project.forge_version}"

	// Oh great create
	modImplementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }

	// Botarium for energy API
	modImplementation "earth.terrarium:botarium-common-${minecraft_version}:${botarium_version}"
	modApi("earth.terrarium:botarium-forge-${minecraft_version}:${botarium_version}")

	// more stuff
	modImplementation("com.tterrag.registrate:Registrate:${registrate_version}")
	modImplementation("com.jozufozu.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}")

	// For testing electricity stuff, don't actually use classes provided by it
//	 modImplementation("earth.terrarium.adastra:ad_astra-fabric-$minecraft_version:$ad_astra_version")

	modCompileOnly("dev.emi:emi-forge:${emi_version}");
	compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
	compileOnly("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rei_version}");


	switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
		case "jei": modLocalRuntime("maven.modrinth:jei:${jei_runtime}"); break
		case "rei": modLocalRuntime("me.shedaniel:RoughlyEnoughItems-forge:${rei_version}"); break
		case "emi": modLocalRuntime("dev.emi:emi-forge:${emi_version}"); break
		case "disabled": break
		default: println("Unknown recipe viewer specified: ${recipe_viewer}. Must be JEI, REI, EMI, or disabled.")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
    manifest {
        attributes([
                "Specification-Title"     : "create-new-age",
                "Specification-Vendor"    : "AnatarcticGardens",
                "Specification-Version"   : "1",
                "Implementation-Title"    : "Create: New Age",
                "Implementation-Version"  : "1",
                "Implementation-Vendor"   : "AnatarcticGardens",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}