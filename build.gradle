plugins {
    id "java"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "dev.architectury.loom"
    
    group = "org.antarcticgardens"
    version = rootProject.modVersion
    archivesBaseName = "create-new-age-${version}-${project.name}-${rootProject.minecraftVersion}"
    
    repositories {
        mavenLocal()
        
        maven { url = "https://cursemaven.com" } // CurseForge maven
        maven { url = "https://api.modrinth.com/maven" } // Modrinth maven
        
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.tterrag.com/" } // Create Forge, Flywheel, Forge Registrate
        maven { url = "https://mvn.devos.one/snapshots/" } // Create Fabric, Fabric Registrate 
        
        maven { url = "https://maven.blamejared.com/" } // JEI
        
        // Create dependencies
        maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
        
        maven { url = "https://jitpack.io/" } // Mixin Extras
    }
    
    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraftVersion}"
        
        mappings(loom.layered {
            it.officialMojangMappings()
            it.parchment("org.parchmentmc.data:parchment-${rootProject.minecraftVersion}:${rootProject.parchmentVersion}@zip")
        })
        
        annotationProcessor("systems.manifold:manifold-preprocessor:2024.1.2")
        
        modImplementation("org.antarcticgardens:esl-${project.name}:${rootProject.eslVersion}")
        
        if (project.name != "fabric") { 
            modApi("com.tterrag.registrate:Registrate:${rootProject.registrateVersion}")
            modImplementation("com.jozufozu.flywheel:flywheel-forge-${rootProject.minecraftVersion}:${rootProject.flywheelVersion}")
            modImplementation("com.simibubi.create:create-${rootProject.minecraftVersion}:${rootProject.createVersion}:slim") { transitive = false } 
        }
        
        modCompileOnlyApi("mezz.jei:jei-${rootProject.minecraftVersion}-common-api:${rootProject.jeiVersion}")
        
        if (project.name != "common") {
            if (recipeViewer == "jei") {
                modRuntimeOnly("mezz.jei:jei-${rootProject.minecraftVersion}-${project.name}:${rootProject.jeiVersion}")
            }
        }
    }
    
    if (project.name != "common") {
        processResources {
            from(project(":common").sourceSets.main.resources)
            
            filesMatching(["fabric.mod.json", "META-INF/mods.toml", "assets/**/*.json"]) {
                expand(project.properties)
            }
        }
    }
    
    loom {
        accessWidenerPath = project(":common").file("src/main/resources/create_new_age-common.accesswidener")
    }
    
    sourceSets {
        main {
            resources {
                srcDirs += [
                        'src/generated/resources'
                ]
            }
        }
    }
    
    jar {
        archiveClassifier.set("dev")
    }
    
    java {
        withSourcesJar()
    }
    
    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xplugin:Manifold']
    }
}
